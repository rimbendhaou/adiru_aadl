package Adiru
public

	with subsystem;
	with adiru_buses;
	with adiru_memory;
	with other_system;

	------------------------------------------------------------------------

	-- AADL ADIRU MODEL

	------------------------------------------------------------------------
	

	-------------------------------------------------------------------------
	--  Root system ADIRU
	-------------------------------------------------------------------------
	system ADIRU
	end ADIRU;

	system implementation ADIRU.impl

		subcomponents
		    -- memory
			mem: memory adiru_memory::main_memory.impl;

			-- modules subsystems
			module_ir: system subsystem::ir_system.impl;
			module_adr: system subsystem::adr_system.impl;

			-- bus
			afdx_network: bus adiru_buses::physical_afdx_link.impl;

			-- devices
			cdu: device subsystem::cdu;
			temperature_sensor_rdc: device subsystem::RDC_temperature_sensor;
			pitot_static_sensor_rdc: device subsystem::RDC_pitot_static_sensor;
			--switch
			--sw1: device adiru_buses::afdx_switch;

			FMC: system other_system::fmc_system.impl;

		connections
		    --connections with CDU
			cn1: port cdu.latitude -> module_ir.set_latitude_in;
			cn2: port cdu.longitude -> module_ir.set_longitude_in;
			cn3: port cdu.magnetic_heading -> module_ir.set_magnetic_heading_in;
			cn17: port  module_adr.indicated_AOA -> cdu.indicated_AOA;
			cn18 : port cdu.m1_disc_output -> module_ir.m1_disc;
            --cn19 : port cdu.m2_disc_output -> module_ir.m2_disc;
            
			--connections with FMC
			cn4: port module_ir.present_pos_lat_out -> FMC.present_pos_lat_in;
			cn5: port module_ir.present_pos_long_out -> FMC.present_pos_long_in;
			cn6: port module_ir.ground_speed_out -> FMC.ground_speed_in;
			cn7: port module_ir.track_angle_true_D -> FMC.track_angle_true_D;
			cn8: port module_ir.wind_speed_D -> FMC.wind_speed_D;

			--connections with other ADR portion of other ADIRU
			cn9: port module_adr.altitude -> module_ir.altitude_in;
			cn10: port module_adr.true_airspeed -> module_ir.true_airspeed_in;

			--connections with RDC temperature sensor
			cn11: port temperature_sensor_rdc.tot_temp -> module_adr.tat;
			cn12: port temperature_sensor_rdc.static_air_temp -> module_adr.sat;
			--connections with RDC pitot-static sensor
			cn13: port pitot_static_sensor_rdc.true_airspeed -> module_adr.true_airspeed;
			cn14: port pitot_static_sensor_rdc.tot_pressure -> module_adr.tot_pressure;
			cn15: port pitot_static_sensor_rdc.ground_speed -> module_adr.ground_speed;
			cn16: port  pitot_static_sensor_rdc.indicated_AOA -> module_adr.indicated_AOA;
			
			

			-- busses accesses
			ir_module_afdxAccess: bus access afdx_network -> module_ir.afdx_access;
			adr_module_afdxaccess: bus access afdx_network -> module_adr.afdx_access;
			pit_stat_rdc_afdxAccess: bus access afdx_network -> pitot_static_sensor_rdc.afdx_access;
			temperature_rdc_afdxAccess: bus access afdx_network -> temperature_sensor_rdc.afdx_access;

		properties
			Actual_connection_binding => (reference (afdx_network.VL1)) applies to cn1;
			Actual_connection_binding => (reference (afdx_network.VL2)) applies to cn2;
			Actual_connection_binding => (reference (afdx_network.VL3)) applies to cn3;
			Actual_connection_binding => (reference (afdx_network.VL4)) applies to cn4;
			Actual_connection_binding => (reference (afdx_network.VL5)) applies to cn5;
			Actual_connection_binding => (reference (afdx_network.VL6)) applies to cn6;

			Actual_Memory_Binding => (reference (mem.irMem)) applies to module_ir.cpu;
			Actual_Memory_Binding => (reference (mem.adrMem)) applies to module_adr.cpu;
			

	end ADIRU.impl;

end Adiru;