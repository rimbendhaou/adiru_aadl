package subsystem
public
	with adiru_data;
	with adiru_memory;
	with adiru_buses;
	with adiru_processor;
	with adiru_software;

	-----------------------------------------------------------------
	-------------------------devices---------------------------------
	-----------------------------------------------------------------
	device cdu
		features
			
		    -- cdu on off
			on_off_cdu: out event data port adiru_data::on_off_cdu;
			-- Air Data on off discrete output
			ad_on_off_cdu: out event data port adiru_data::ad_on_off_cdu;
			-- IR off command
			ir_off_cmd: out event data port adiru_data::ir_off_cmd;
			-- M1 and M2 discrete outputs to specify mode (OFF, ALIGN, ATT, NAV)
			m1_disc_output: out event data port adiru_data::m1_disc_output;
			m2_disc_output: out event data port adiru_data::m2_disc_output;

			-- data output and input
			latitude : in out event data port adiru_data::set_latitude_041;
			longitude : in out event data port adiru_data::set_longitude_042;
			magnetic_heading : in out event data port adiru_data::set_magnetic_heading_043;
			
			indicated_AOA : in data port adiru_data::indicated_AOA;

			-- TODO add data input :  inserting present position, latitude and longitude and, heading set for the IR portion


					-- hardware interface
					--can_access  : requires bus access fms_hardware::can;
					--flows
					--disp_sink : flow sink ph_disp;

	end cdu;


    -- RDC - Remote Data Concentrator: equipment supporting the exchange of information between sensors / actuators (digital, discrete and analogue data) and aircraft digital communication networks (ADCN). 
	-- The RDCs are located in pressurized areas close to sensors and effectors, which may be potentially remote from the associated processing resources, then not in the avionics bay. 
	--The above mentioned hardware modules should mainly communicate between each other using AFDX. 
	--The message format should follow ARINC 664 specification. 
	-------------------------------------------------------------------------
	
	device RDC_temperature_sensor
	features
			tot_temp : out event data port adiru_data::tat;
			static_air_temp : out event data port adiru_data::sat_213_233;
			afdx_access  : requires bus access adiru_buses::physical_afdx_link.impl;

	end RDC_temperature_sensor;
	
	device RDC_pitot_static_sensor
	features
			true_airspeed : out event data port adiru_data::true_airspeed_210;
			tot_pressure : out event data port adiru_data::t_pressure_242;
			ground_speed : out event data port adiru_data::ground_speed_D_012;
			indicated_AOA : out event data port adiru_data::indicated_AOA;
			afdx_access  : requires bus access adiru_buses::physical_afdx_link.impl;
			

	end RDC_pitot_static_sensor;

	------------------------------------------------------------------
	------------------------------------------------------------------
	--------------------- modules systems-----------------------------
	------------------------------------------------------------------

	-----------------------------------------------------------------------
	--------------------- IR portion of the ADIRU -------------------------
	-- the IRS provides:
	-- basic outputs for aircraft angular rate and acceleration, 
	-- and computed outputs of attitude, true heading, magnetic heading, velocity and present position
	-- It should provide outputs of inertial position, velocity, drift angle and wind data
	-----------------------------------------------------------------------
	system ir_system
		features
			------------------------------------------------------------------------------
			-- inputs
			------------------------------------------------------------------------------
		    -- data initialization from CDU
			set_latitude_in: in event data port adiru_data::set_latitude_041;
			set_longitude_in: in event data port adiru_data::set_longitude_042;
			set_magnetic_heading_in: in event data port adiru_data::set_magnetic_heading_043;

            -- mode selection
            m1_disc : in event data port adiru_data::m1_disc_output;
            m2_disc : in event data port adiru_data::m2_disc_output;
            
			--Air data portion of other ADIRU provides these info
			altitude_in: in data port adiru_data::altitude_203;
			true_airspeed_in: in data port adiru_data::true_airspeed_210;
			

			------------------------------------------------------------------------------
			-- outputs
			------------------------------------------------------------------------------
		    
		    --to IRS, adirs,A705
			inertial_altitude_out: out data port adiru_data::inertial_altitude_361;
			-- inertial vertical speed : label 365 : to IRS, adirs,A705 
			inertial_vs_out: out data port adiru_data::inertial_vs_365;
			-- to IRS, adirs,FMC A702 
			present_pos_lat_out: out data port adiru_data::present_pos_lat_010;
			--present position long: to IRS, adirs,FMC A702 
			present_pos_long_out: out data port adiru_data::present_pos_long_011;
			-- to other adiru, FMC, Electronic Flight instrument 725, GNSS 756, Gnss 760
			ground_speed_out: out data port adiru_data::ground_speed_D_012;
			--to adiru,IRS,FMC
			track_angle_true_D: out data port adiru_data::track_angle_true_D_013;
			-- to adiru,IRS
			magnetic_heading: out data port adiru_data::magnetic_heading_014;
			--to adiru,fmc
			wind_speed_D: out data port adiru_data::wind_speed_D_015;
			--to adiru
			wind_direction_true_D: out data port adiru_data::wind_direction_true_D_016;
			true_heading_D: out data port adiru_data::true_heading_D_044;

			-- in ATT mode IR should maintain pitch and roll output signals
			body_pitch_acc_out: out data port adiru_data::body_pitch_acc_052;
			body_roll_acc_out: out data port adiru_data::body_roll_acc_053;
			body_yaw_acc_out: out data port adiru_data::body_yaw_acc_054;
			--IR should continue to function as a source of aircraft attitude data for pitch and roll
			-- an azimuth output should also be provided in ATT mode


			-- hardware interface
			afdx_access: requires bus access adiru_buses::physical_afdx_link.impl;

	end ir_system;

	system implementation ir_system.impl

		subcomponents
			ir_memory: memory adiru_memory::partition_memory.i;
			cpu: processor adiru_processor::ir_processor.impl;
			ir_process: process adiru_software::prs_IR.impl;
			
		connections
			--connections with FMC or CDU
			ir_cn1: port set_latitude_in -> ir_process.set_latitude;
			ir_cn2: port set_longitude_in -> ir_process.set_longitude;
			ir_cn3: port set_magnetic_heading_in -> ir_process.set_magnetic_heading;
			ir_cn4: port altitude_in -> ir_process.altitude_in;
			ir_cn5: port true_airspeed_in -> ir_process.true_airspeed_in;
			
			--
			ir_cn6: port ir_process.inertial_altitude_out->inertial_altitude_out;
			-- inertial vertical speed 
			ir_cn7 : port ir_process.inertial_vs_out->inertial_vs_out;
			--present position latitude
			ir_cn8 : port ir_process.present_pos_lat_out->present_pos_lat_out;
			--present position long
			ir_cn9 : port ir_process.present_pos_long_out->present_pos_long_out;
			-- 
			ir_cn10 : port ir_process.ground_speed_out->ground_speed_out;
			--
			ir_cn11 : port ir_process.track_angle_true_D->track_angle_true_D;
			-- 
			ir_cn12 : port ir_process.magnetic_heading->magnetic_heading;
			--
			ir_cn13 : port ir_process.wind_speed_D->wind_speed_D;
			--
			ir_cn14 : port ir_process.wind_direction_true_D->wind_direction_true_D;
			ir_cn15 : port ir_process.true_heading_D->true_heading_D;

			-- 
			ir_cn16 : port ir_process.body_pitch_acc_out->body_pitch_acc_out;
			ir_cn17 : port ir_process.body_roll_acc_out->body_roll_acc_out;
			ir_cn18 : port ir_process.body_yaw_acc_out->body_yaw_acc_out;
			

	end ir_system.impl;

	-----------------------------------------------------------------------
	--------------------- ADR portion of the ADIRU -------------------------
	-----------------------------------------------------------------------
	system adr_system
		features
			altitude : out data port adiru_data::altitude_203;
			mach : out data port adiru_data::mach_205;
			altitude_rate : out data port adiru_data::altitude_rate_212;
			-- total air temperature : label 211
			tat : in out data port adiru_data::tat;
			indicated_AOA : in out data port adiru_data::indicated_AOA;
			baro_corr_altitude1 : out data port adiru_data::baro_corr_altitude1_204;
			baro_corr_altitude2 : out data port adiru_data::baro_corr_altitude2_204;
			computed_air_speed : out data port adiru_data::computed_air_speed_206;
			true_airspeed: in out data port adiru_data::true_airspeed_210;
			ground_speed:in out data port adiru_data::ground_speed_D_012;
			--static air temperature
			sat : in out data port adiru_data::sat_213_233;
			impact_pressure: out data port adiru_data::impact_pressure_215;
			--total pressure
			tot_pressure : in out data port adiru_data::t_pressure_242;
			corrected_AOA : out data port adiru_data::corrected_AOA_241;
			discrete_word1: out data port adiru_data::discrete_word1_270;
			discrete_word2: out data port adiru_data::discrete_word2_271;


			-- hardware interface
			afdx_access  : requires bus access adiru_buses::physical_afdx_link.impl; 

	end adr_system;

	system implementation adr_system.impl

		subcomponents
			adr_memory: memory adiru_memory::main_memory.impl;
			cpu : processor adiru_processor::adr_processor.impl;
			adr_process : process adiru_software::prs_ADR.impl;
			
		connections
			adr_cn1: port adr_process.altitude->altitude ;
			adr_cn2: port adr_process.mach->mach;
			adr_cn3: port adr_process.altitude_rate->altitude_rate;
			-- total air temperature 
			adr_cn4: port tat-> adr_process.tat;
			adr_cn5: port adr_process.indicated_AOA->indicated_AOA;
			adr_cn6: port adr_process.baro_corr_altitude1->baro_corr_altitude1;
			adr_cn7: port adr_process.baro_corr_altitude2->baro_corr_altitude2;
			adr_cn8: port adr_process.computed_air_speed->computed_air_speed;
			adr_cn9: port adr_process.true_airspeed->true_airspeed;
			adr_cn10: port adr_process.ground_speed->ground_speed;
			--static air temperature
			adr_cn11: port adr_process.sat->sat;
			adr_cn12: port adr_process.impact_pressure->impact_pressure;
			--total pressure
			adr_cn13: port adr_process.tot_pressure->tot_pressure;
			adr_cn14: port adr_process.corrected_AOA->corrected_AOA;
			adr_cn15: port adr_process.discrete_word1->discrete_word1;
			adr_cn16: port adr_process.discrete_word2->discrete_word2;


	end adr_system.impl;

end subsystem;